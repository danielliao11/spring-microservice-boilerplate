description 'spring-microservices-boilerplate'
group 'com.saintdan.framework'
version '1.2.2.RELEASE'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven' // If you want use maven.
//apply plugin: 'war' // If you want use provide runtime dependencies.

buildscript {
  repositories {
    // If you live in China. You can use ali's repository.
    maven {
      url 'http://maven.aliyun.com/nexus/content/groups/public/'
    }
    maven {
      url 'https://repo.spring.io/plugins-release'
    }
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath('org.springframework.boot:spring-boot-gradle-plugin:2.0.0.M4')
  }
}

allprojects {
  repositories {
    // If you live in China. You can use ali's repository.
    maven {
      url 'http://maven.aliyun.com/nexus/content/groups/public/'
    }
    maven {
      url 'https://repo.spring.io/plugins-release'
    }
    mavenCentral()
    jcenter()
  }
}

compileJava {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}

compileTestJava {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}

ext {
  springBootVersion = '2.0.0.M5'
  springOAuth2Version = '2.2.0.RELEASE'

  lombokVersion = '1.16.18'
  commonsTextVersion = '1.1'
  commonsLang3Version = '3.6'
  commonsBeanutilsVersion = '1.9.3'
  guavaVersion = '21.0'
  swaggerVersion = '2.7.0'
  specVersion = '0.9.2'

//  hsqldbVersion = '1.8.0.10'
//  mysqlVersion = '5.1.36'
  postgresqlVersion = '42.1.4'

  jsonPathVersion = '2.0.0'
  junitVersion = '4.12'
}

dependencies {
  // Spring boot dependencies.
  compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
  compile "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
  compile "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
  compile "org.springframework.boot:spring-boot-devtools:${springBootVersion}"

  // Spring oauth2 dependency.
  compile ("org.springframework.security.oauth:spring-security-oauth2:${springOAuth2Version}") {
    exclude group: 'org.codehaus.jackson'
  }

  // Other dependencies.
  compile "org.projectlombok:lombok:${lombokVersion}"
  compile "org.apache.commons:commons-lang3:${commonsLang3Version}"
  compile "org.apache.commons:commons-text:${commonsTextVersion}"
  compile "commons-beanutils:commons-beanutils:${commonsBeanutilsVersion}"
  compile "com.google.guava:guava:${guavaVersion}"
  compile ("net.kaczmarzyk:specification-arg-resolver:${specVersion}") {
    exclude group:'org.hibernate.javax.persistence'
  }

  //swagger
  compile "io.springfox:springfox-swagger2:${swaggerVersion}"
  compile "io.springfox:springfox-swagger-ui:${swaggerVersion}"

  // DB dependencies.
  // You can choose one of them.
  // HSQLDB
//  compile "org.hsqldb:hsqldb:${hsqldbVersion}"

  // Mysql
//  compile "mysql:mysql-connector-java:${mysqlVersion}"

  // Postgres
  compile "org.postgresql:postgresql:${postgresqlVersion}"

  // Provide runtime dependencies.
  // Tomcat
//  providedRuntime "org.springframework.boot:spring-boot-starter-tomcat:${springBootVersion}"
  // Undertow - Undertow is a lightweight servlet container from JBoss
//  providedRuntime "org.springframework.boot:spring-boot-starter-undertow:${springBootVersion}"

  // Test dependencies.
  testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
  testCompile "com.jayway.jsonpath:json-path:${jsonPathVersion}"
  testCompile "com.jayway.jsonpath:json-path-assert:${jsonPathVersion}"
  testCompile "junit:junit:${junitVersion}"
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.5'
}

task writeNewPom {
  doLast {
    pom {
      project {
        inceptionYear '2017'
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }
      }
    }.writeTo("pom.xml")
  }
}